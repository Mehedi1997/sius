apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

group = 'org.beyene'
archivesBaseName = 'sius'
version = '0.2.0-SNAPSHOT'

sourceSets {
    main {
        java {
            exclude 'org/beyene/sius/example/**'
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Sius', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(JavaCompile) { 
    options.compilerArgs << '-Xlint:all'
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'org.beyene:sius'
        packaging 'jar'
        artifactId 'sius'
        description 'S.I. Unit System Conversion Library'
        url 'https://github.com/mbe24/sius'

        scm {
          connection 'scm:git@github.com:mbe24/sius.git'
          developerConnection 'scm:git@github.com:mbe24/sius.git'
          url 'https://github.com/mbe24/sius'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'mbeyene'
            name 'Mikael Beyene'
            email 'dev@beyene.org'
          }
        }
      }
    }
  }
}